#!@PERL@ -w
# -*-CPerl-*-
# $Id$
#
# Copyright (C) 2001-2002 Masao Takaku All rights reserved.
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
# This file must be encoded in EUC-JP encoding.
#

use IO::File;
use Getopt::Long;
use Cwd;
use Text::Tabs;
use MIME::Base64;
use MIME::QuotedPrint;

use strict;

my $DEBUG = undef;
my $PACKAGE = "@PACKAGE@";
my $VERSION = "@VERSION@";

my $Printer = "lp";
$Printer = "$ENV{'PRINTER'}" if defined($ENV{'PRINTER'});

my $Tmpdir = "/tmp";
$Tmpdir = "$ENV{'TMPDIR'}" if defined($ENV{'TMPDIR'});
$Tmpdir .= "/mnewsprint-$$";

my $File = "-";		# default is STDIN

my $PrintHeaders = "date,from,to,cc,newsgroups,subject";

my $SUPPORT_XFACE = "@UNCOMPFACE@";
$SUPPORT_XFACE = undef unless $SUPPORT_XFACE =~ /uncompface/i;

my $SUPPORT_PSNUP = "@PSNUP@";
$SUPPORT_PSNUP = undef unless $SUPPORT_PSNUP =~ /psnup/i;

my $PsnupOpt = 'auto';

my %Bold = ('subject' => 1);

main();
sub main {
    parse_options();
    my $fh = fopen($File);
    my @tmp = <$fh>;
    chomp(@tmp);
    my $mail = parse_message(@tmp);
    fork && exit;	# Go background
    mkdir($Tmpdir, 0700) || die "can't create $Tmpdir: $!";
    create_latex($mail);
    compile_latex();
}

sub parse_options() {
    my $opt_help = undef;
    my $opt_version = undef;

    GetOptions ('f|file=s'	      => \$File,
		'P|printer=s'	      => \$Printer,
		'H|header=s'	      => \$PrintHeaders,
		'psnup'		      => \$PsnupOpt,
		'h|help'              => \$opt_help,
		'd|debug'             => \$DEBUG,
                'version'             => \$opt_version,
                ) || usage(1);

    usage(0) if ($opt_help);

    if ($opt_version) {
        print "$PACKAGE $VERSION\n";
	exit 0;
    }
}

sub usage($) {
    my ($status) = @_;
    print <<EOF;

    Usage: mnewsprint [--printer=PRINTER] [--file=FILE] [--header=LIST]
    [--help] [--version]

EOF
    exit($status);
}

sub parse_message(@) {
    my (@tmp) = @_;
    my %header = ();
    my $body = '';
    while (@tmp) {
	my $line = shift @tmp;
	last if ($line =~ /^$/);

	while (defined($tmp[0]) && $tmp[0] =~ /^\s+/) {
            my $nextline = shift @tmp;
            $line =~ s/([\xa1-\xfe])\s+$/$1/;
            $nextline =~ s/^\s+([\xa1-\xfe])/$1/;
            $line .= $nextline;
	}

	if ($line =~ /^(\S+):\s*(.*)$/) {
	    $header{lc($1)} = $2;
	}
    }
    if (defined($DEBUG)) {
	foreach my $key (keys %header) {
  	    print " $key -> $header{$key}\n";
	}
    }
    if (defined($header{'content-type'}) &&
	$header{'content-type'} =~ m#^multipart/#i &&
	$header{'content-type'} =~ m#boundary=(\")?(.+)\1#i) {
	print STDERR "multipart processing ...\n";
	my $boundary = quotemeta($2);
	while (@tmp) {
	    my $line = shift @tmp;
	    last if $line =~ /^--$boundary/;
	}
	my @part = ();
	$body = [];
	for my $line (@tmp) {
	    if ($line =~ /^--$boundary--$/) {
		push @{$body}, parse_message(@part);
		last;
	    } elsif ($line =~ /^--$boundary$/) {
		push @{$body}, parse_message(@part);
		@part = ();
	    } else {
		push @part, $line;
	    }
	}
    } else {
	my $decoded_str = join("\n", @tmp);
	if (defined($header{'content-transfer-encoding'}) &&
	    $header{'content-transfer-encoding'} =~ /base64/) {
	    $decoded_str = decode_base64($decoded_str);
	} elsif (defined($header{'content-transfer-encoding'}) &&
		 $header{'content-transfer-encoding'} =~ /quoted-printable/) {
	    $decoded_str = decode_qp($decoded_str);
	}
	if (!is_textual_message($header{'content-type'})) {
	    $body = $decoded_str;
	} else {
	    @tmp = split("\n", $decoded_str);
	    @tmp = expand(@tmp);
	    for my $line (@tmp) {
		$line =~ s/\s+$//;
		$line = split_line($line, 78);
		$line = escape_specials($line);
		# Don't do that if there is an HTML tag at first.
		if ($line !~ /^[^>]*</ && 
		    $line =~ /^((\s*\S{1,10}>)|(\s*[\>\|\:]+\s*))+/) {
		    $line =~ s/^/\\textcolor\{q\}\{/;
		    $line =~ s/$/\}/;
		}
		$body .= $line . "\n";
	    }
	    $body =~ s/\s+$//;
	}
    }
    return {"body" => $body, "header" => \%header};
}

sub create_latex(\%) {
    my ($mail) = @_;
    my $texfile = "$Tmpdir/mail.tex";
    my $header_str = format_header($$mail{'header'});
    my $realname = get_realname($$mail{'header'});
    my $subject = $$mail{'header'}{'subject'};

    my $p_width = "145mm";
    my $face_text = "";
    if (defined $$mail{'header'}{'x-face'} && defined($SUPPORT_XFACE)) {
	$face_text = process_xface($$mail{'header'});
	$p_width = "120mm";
    }
    my $fh = fopen("> $texfile");
    print $fh <<EOF;
\\documentclass{@DOCUMENT_CLASS@}
\\usepackage[T1]{fontenc}
\\usepackage[latin1]{inputenc}
\\usepackage{cmbright}
\\usepackage[a4paper,left=20mm,right=20mm,top=18mm,bottom=25mm,headsep=5mm]{geometry}
\\usepackage{lastpage,fancyhdr,scrtime,graphicx,alltt}
\\usepackage[dvips]{color}
\\definecolor{q}{gray}{0.4}
\\def\\ast{{\\tt\\symbol{"2A}}}
\\def\\bs{{\\tt\\symbol{"5C}}}
\\setlength{\\parindent}{0mm}
\\renewcommand{\\baselinestretch}{.85}
\\pagestyle{fancy}
\\lhead{\\itshape $realname}
\\rhead{\\bfseries $subject}
\\cfoot{}
\\lfoot{\\itshape\\number\\year-\\number\\month-\\number\\day~~\\thistime}
\\rfoot{\\itshape Page~\\thepage~/~\\pageref{LastPage}}
\\renewcommand{\\headrulewidth}{0pt}
\\fancypagestyle{plain}{%
\\fancyhf{}
\\setlength{\\headsep}{1mm}
\\addtolength{\\footskip}{4mm}
\\lfoot{\\itshape\\number\\year-\\number\\month-\\number\\day~~\\thistime}
\\rfoot{\\itshape Page~\\thepage~/~\\pageref{LastPage}}}
\\begin{document}
\\thispagestyle{plain}
\\large
\\begin{tabular}[t]{rp{$p_width}l}
$header_str
\\end{tabular}
\\hfill
$face_text
\\vspace{5mm} \\hrule \\vspace{5mm}
EOF
    create_latex_sub($fh, $mail);
    print $fh "\\end{document}\n";
    close($fh);
}

sub create_latex_sub($$) {
    my ($fh, $mail) = @_;
    if (ref($$mail{'body'}) =~ /^ARRAY/) {
	foreach my $part (@{$$mail{'body'}}) {
	    create_latex_sub($fh, $part);
	    print $fh "\\vspace{3mm} \\hrule \\vspace{3mm}";
	}
    } else {
	if (!is_textual_message($$mail{'header'}{'content-type'})) {
	    print $fh "\\begin{tabular}[t]{rl}\n";
	    print $fh ("\\bfseries Size: & \\bfseries ",
		       length($$mail{'body'}), " bytes\\\\\n");
	    foreach my $h (sort keys %{$$mail{'header'}}) {
		print $fh (header_ucfirst($h), " & ",
			   escape_specials($$mail{'header'}{$h}), "\\\\\n");
	    }
	    print $fh "\\end{tabular}\n";
	} else {
	    print $fh "\\begin{alltt}$$mail{'body'}\\end{alltt}\n";
	}
    }
}

sub process_xface(%) {
    my ($header) = @_;
    my $fh = fopen("> $Tmpdir/face_tmp");
    print $fh $$header{'x-face'};
    $fh->close;
    # cat face_tmp | ( echo '/* Width=48, Height=48 */' ; uncompface ) | icontopbm | pnmtops > z.ps
    my $face_decoded = `@UNCOMPFACE@ $Tmpdir/face_tmp`;
    $face_decoded =~ s/(0x|,|\s+)//gmo;
    $face_decoded =~ s/([0-9a-f])/sprintf("%x", hex("f") - hex($1))/gei;
    # print STDERR "$face_decoded\n";

    $fh = fopen("> $Tmpdir/face.ps");
    print $fh <<EOF;
%!PS-Adobe-2.0 EPSF-2.0
%%Creator: @PACKAGE@
%%Title: face.ps
%%Pages: 1
%%BoundingBox: 282 357 330 405
%%EndComments
%%Page: 1 1
282 357 translate
48 48 scale
48 48 1
[ 48 0 0 -48 0 48 ]
{ <
$face_decoded
> } image
showpage
%%Trailer
EOF

    my $face_text = <<EOF;
\\raisebox{4mm}{
\\begin{minipage}[t]{2cm}
\\begin{flushright}
\~ \\\\
\\includegraphics[height=2cm]{face.ps}
\\end{flushright}
\\end{minipage}}
EOF
    return $face_text;
}

sub compile_latex() {
    my $cwd = getcwd();
    chdir($Tmpdir);

    for (1, 2) {
	system("@LATEX@ -interaction=nonstopmode mail.tex > /dev/null 2>&1");
    }

    system("@DVIPS@ -o mail.ps mail.dvi > /dev/null 2>&1");

    if (defined($SUPPORT_PSNUP) && $PsnupOpt ne 'none') {
	if ($PsnupOpt eq 'auto') {
	    my $lastpage = get_lastpage();
	    $PsnupOpt = "";
	    $PsnupOpt = "-2" if $lastpage > 1;
	    $PsnupOpt = "-4" if $lastpage > 2;
	}
	system("$SUPPORT_PSNUP $PsnupOpt mail.ps nup.ps > /dev/null 2>&1");
	rename("nup.ps", "mail.ps") || die "rename failed: $!";
    }

    system("lpr -P$Printer mail.ps") if (!defined($DEBUG));

    chdir($cwd);
    if (!defined($DEBUG)) {
	unlink(glob("$Tmpdir/*")) || warn "unlink: $!";
	rmdir($Tmpdir) || warn "rmdir: $!";
    }
}

sub get_realname(%) {
    my ($header) = @_;
    return '' if not defined $$header{'from'};

    my $from = '';
    if ($$header{'from'} =~ /^\"?(.+)\"?\s*<(\S+)\@(\S+)>/) {
	$from = $1;
    } elsif ($$header{'from'} =~ /^(\S+)\@(\S+)\s*\(([^)]+)\)/) {
	$from = $3;
    } else {
	$from = $$header{'from'};
    }
    return $from;
}

sub get_lastpage() {
    my $fh = fopen("mail.aux");
    while (defined(my $line = <$fh>)) {
	if ($line =~ /^\\newlabel{LastPage}{{}{(\d+)}}$/i) {
	    return $1;
	}
    }
    return 0;
}

sub format_header(%) {
    my ($header) = @_;
    my $header_str = '';
    foreach my $h (split(/,/, lc($PrintHeaders))) {
	if (defined($$header{$h})) {
	    $$header{$h} = escape_specials($$header{$h});
	    $header_str .= "\\bfseries " if (defined($Bold{$h}));
	    $header_str .= header_ucfirst($h) . ": & ";
	    $header_str .= "\\bfseries " if (defined($Bold{$h}));
	    $header_str .= "$$header{$h} & \\\\\n";
	} else {
	    $header_str .= "\n";
	}
    }
    return $header_str;
}

# Content-Type
sub is_textual_message ($) {
    my ($type) = @_;
    return 1 unless defined $type;
    if ($type =~ m#^(text|message)#i) {
	return 1;
    } else {
	return undef;
    }
}

sub header_ucfirst($) {
    my ($h) = @_;
    return join('-', map { ucfirst } split('-', $h));
}

# Format a line to fixed-chars per line.
sub split_line($$) {
    my ($text, $chars_perline) = @_;

    my $res = '';
    while (length($text) > $chars_perline) {
	my $tmp = substr($text, 0, $chars_perline);
	my $tmp2 = substr($text, $chars_perline);
	# considering multi-byte segmentation.
	if ($tmp =~ /\x8f$/ or $tmp =~ tr/\x8e\xa1-\xfe// % 2) {
	    $tmp2 = chop($tmp) . $tmp2;
	    $tmp2 = chop($tmp) . $tmp2 if ($tmp =~ /\x8f$/);
	}
	$res .= $tmp . "\n";
	$text = $tmp2;
	print "\$tmp: $tmp\n" if defined($DEBUG);
	print "\$tmp2: $tmp2\n" if defined($DEBUG);
    }
    $res .= $text;
    return $res;
}

sub escape_specials($) {
    my ($text) = @_;
    my %specials = ('#' => '\\#',
		    '$' => '\\$',
		    '&' => '\\&',
		    '%' => '\\%',
		    '*' => '\\ast{}',
		    '\\' => '\\bs{}',
		    '^' => '\\^{}',
		    '_' => '\\_{}',
		    '{' => '\\{',
		    '}' => '\\}',
		    '~' => '\\~{}',
		   );
    $text =~ s/([#\$&%*\\^_{}~])/$specials{$1}/ge;
    return $text;
}

sub fopen($) {
    my ($file) = @_;
    my $fh = new IO::File;
    $fh->open("$file") || die "$file: $!";

    return $fh;
}

__END__

=head1 NAME

mnewsprint - E-mail メッセージの整形印刷ツール

=head1 SYNOPSIS

mnewsprint [B<--printer=PRINTER>] [B<--file=FILE>] [B<--header=LIST>]
    [B<--psnup=OPTION>] [B<--help>] [B<--version>]

=head1 DESCRIPTION

mnewsprintはE-mailメッセージを整形して印刷するスクリプトです。

=head1 OPTIONS

以下のオプションを指定できます。

=over 4

=item B<-P>, B<--printer=PRINTER>

出力するプリンタ名を指定します。
（デフォルトでは PRINTER 環境変数または "lp" です）

=item B<-f>, B<--file=FILE>

印刷すべきファイルを指定します。
（デフォルトでは標準入力から読み込みます）

=item B<-H>, B<--header=LIST>

出力すべきメール・ヘッダを指定します。
（デフォルトでは "date,from,to,cc,newsgroups,subject" です）

=item B<--psnup=OPTION>

psnup コマンドを使って整形するためのオプションを指定します。
（デフォルトでは "auto" です）

"auto" の場合は、ページ数に応じて、-2, -4 などのレイアウトを自動的に行
います。また、 "none" の場合は、psnup コマンドを利用しません。それ以外
の場合は、そのまま psnup コマンドのオプションになります。常に一定のレ
イアウトを指定したい場合は、--psnup=-2 などのように指定してください。

=back

=head1 PREREQUISITES

このスクリプトはメールの整形に LaTeX2e を利用します。

=head1 NOTES

メールの日本語の文字コードの変換処理は行っていません。MIME 形式のメールにも未対応です。このスクリプトを利用する前に B<nkf> などのコード変換ツールやメールソフトウェアを利用して EUC-JP に変換してから渡してください。

=head1 AUTHOR

高久 雅生 <masao@ulis.ac.jp>
