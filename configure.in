dnl Process this file with autoconf to produce a configure script.
dnl
dnl configure for mnewsprint package.
dnl $Id$
dnl

AC_INIT(mnewsprint.in)
AM_INIT_AUTOMAKE(mnewsprint, 0.3)

dnl Checks for programs.

AC_PROG_INSTALL

dnl Check for LaTeX2e environment
AC_ARG_WITH(
    latex,
    [  --with-latex=PATH       set latex location [search path]],
    AC_MSG_RESULT(using $with_latex for latex)
    LATEX=$with_latex,
    [AC_PATH_PROGS(LATEX, platex jlatex latex tex, no)]
)
if test "$LATEX" = "no"; then
    AC_MSG_ERROR([
*** LaTeX2e environment are required. (latex)])
fi

AC_PATH_PROG(DVIPS, dvips, no)
if test "$DVIPS" = "no"; then
    AC_MSG_ERROR([
*** LaTeX2e environment are required. (dvips)])
fi

dnl check for document class for LaTeX2e
AC_ARG_WITH(
    document-class,
    [  --with-document-class=CLS    set document class in LaTeX [search path]],
    [DOCUMENT_CLASS="$with_document_class"
    AC_MSG_RESULT(using \\documentclass{$DOCUMENT_CLASS} in LaTeX2e)],
    DOCUMENT_CLASS=no
)
if test $DOCUMENT_CLASS = "no"; then
    AC_MSG_CHECKING(for document class in LaTeX2e)
    for cls in jsarticle jarticle article; do
    	if kpsewhich -progname=`basename $LATEX` ${cls}.cls >/dev/null 2>&1; then
	    DOCUMENT_CLASS=$cls
	    break
	fi
    done
    if test "$DOCUMENT_CLASS" = "no"; then
        AC_MSG_ERROR([
*** cannot find document class in LaTeX2e.])
    else
	AC_MSG_RESULT(\\documentclass{$DOCUMENT_CLASS})
    fi
fi

dnl check for style packages
AC_MSG_CHECKING(for style packages in LaTeX2e)
styles="fontenc inputenc cmbright geometry lastpage fancyhdr scrtime graphicx alltt color"
for sty in $styles; do
    if kpsewhich -progname=`basename $LATEX` ${sty}.sty >/dev/null 2>&1; then
        echo $ac_n "${sty} $ac_c"
    else
	AC_MSG_ERROR([
*** cannot find (${sty}.sty)'s package in LaTeX2e.])
    fi
done
echo

dnl check for Perl5
AC_ARG_WITH(
    perl5,
    [  --with-perl5=PATH       set perl5 location [search path]],
    AC_MSG_RESULT(using $with_perl5 for perl5)
    PERL=$with_perl5,
    [AC_PATH_PROGS(PERL, perl5 perl, no)]
)

if test "$PERL" = "no"; then
    AC_MSG_ERROR([
*** Perl5 is required.
*** available from <http://www.perl.com/>])
fi

AC_MSG_CHECKING(for MIME::Base64 perl module)
if $PERL -MMIME::Base64 -e "" > /dev/null 2>&1; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_RESULT(no)
    AC_MSG_ERROR([
*** the MIME::Base64 perl module is required.
*** available from CPAN <http://search.cpan.org/search?dist=MIME-Base64>.])
fi

dnl check for compface package
AC_PATH_PROG(UNCOMPFACE, uncompface, no)
if test "$UNCOMPFACE" = "no"; then
    AC_MSG_WARN([
*** If you need to support 'X-Face:' header, the compface package is required.
*** available from <ftp://syseng.anu.edu.au/pub/jaa/compface.tar.gz>])
fi

dnl check for psutils package
AC_PATH_PROG(PSNUP, psnup, no)
if test "$PSNUP" = "no"; then
    AC_MSG_WARN([
*** If you need to support 2-up printouts, the PSUtils package is required.
*** available from <http://www.tardis.ed.ac.uk/~ajcd/psutils/>])
fi

dnl Substitutions

AC_SUBST(LATEX)
AC_SUBST(DVIPS)
AC_SUBST(DOCUMENT_CLASS)
AC_SUBST(PERL)
AC_SUBST(UNCOMPFACE)
AC_SUBST(PSNUP)

AC_OUTPUT(Makefile tests/Makefile mnewsprint, [chmod +x mnewsprint])
